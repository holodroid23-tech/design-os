apply plugin: 'com.android.application'

android {
    namespace = "com.compostos.app"
    compileSdk = rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.compostos.app"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        aaptOptions {
             // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
             // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern = '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        pickFirst 'META-INF/kotlin-stdlib.kotlin_module'
        pickFirst 'META-INF/kotlin-stdlib-common.kotlin_module'
        pickFirst 'META-INF/kotlin-stdlib-jdk7.kotlin_module'
        pickFirst 'META-INF/kotlin-stdlib-jdk8.kotlin_module'
    }
    
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    flatDir{
        dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
    }
}

dependencies {
    constraints {
        implementation('org.jetbrains.kotlin:kotlin-stdlib-jdk7') {
            because 'kotlin-stdlib-jdk7 is now part of kotlin-stdlib'
            version {
                strictly '1.9.10'
            }
        }
        implementation('org.jetbrains.kotlin:kotlin-stdlib-jdk8') {
            because 'kotlin-stdlib-jdk8 is now part of kotlin-stdlib'
            version {
                strictly '1.9.10'
            }
        }
    }
    
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
    implementation project(':capacitor-android')
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')
    // Stripe Terminal SDK - v5.x with Tap to Pay module
    // All three modules are required for proper AIDL service communication
    implementation "com.stripe:stripeterminal-core:5.1.1"  // Core AIDL service
    implementation "com.stripe:stripeterminal-taptopay:5.1.1"  // Required for Tap to Pay on Android
}

configurations {
    all {
        resolutionStrategy {
            force 'org.jetbrains.kotlin:kotlin-stdlib:1.9.10'
            force 'org.jetbrains.kotlin:kotlin-stdlib-common:1.9.10'
        }
    }
}

apply from: 'capacitor.build.gradle'

try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch(Exception e) {
    logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
